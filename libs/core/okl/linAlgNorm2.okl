/*

The MIT License (MIT)

Copyright (c) 2017-2022 Tim Warburton, Noel Chalmers, Jesse Chan, Ali Karakus

Permission is hereby granted, free of charge, to any person obtaining a copy
of this software and associated documentation files (the "Software"), to deal
in the Software without restriction, including without limitation the rights
to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
copies of the Software, and to permit persons to whom the Software is
furnished to do so, subject to the following conditions:

The above copyright notice and this permission notice shall be included in all
copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
SOFTWARE.

*/

@kernel void norm2(const dlong Nblocks,
                   const dlong N,
                  @restrict const  dfloat *x,
                  @restrict        dfloat *norm){


  for(dlong b=0;b<Nblocks;++b;@outer(0)){

    @shared dfloat s_norm[NORM_BLOCKSIZE];

    for(int t=0;t<NORM_BLOCKSIZE;++t;@inner(0)){
      dlong id = t + b*NORM_BLOCKSIZE;
      dfloat r_norm = 0.0;
      while (id<N) {
        r_norm += x[id]*x[id];
        id += NORM_BLOCKSIZE*Nblocks;
      }
      s_norm[t] = r_norm;
    }

#if NORM_BLOCKSIZE>512
    for(int t=0;t<NORM_BLOCKSIZE;++t;@inner(0)) if(t<512 && t+512<NORM_BLOCKSIZE) s_norm[t] += s_norm[t+512];
#endif
#if NORM_BLOCKSIZE>256
    for(int t=0;t<NORM_BLOCKSIZE;++t;@inner(0)) if(t<256 && t+256<NORM_BLOCKSIZE) s_norm[t] += s_norm[t+256];
#endif
    for(int t=0;t<NORM_BLOCKSIZE;++t;@inner(0)) if(t<128 && t+128<NORM_BLOCKSIZE) s_norm[t] += s_norm[t+128];
    for(int t=0;t<NORM_BLOCKSIZE;++t;@inner(0)) if(t< 64 && t+ 64<NORM_BLOCKSIZE) s_norm[t] += s_norm[t+ 64];
    for(int t=0;t<NORM_BLOCKSIZE;++t;@inner(0)) if(t< 32 && t+ 32<NORM_BLOCKSIZE) s_norm[t] += s_norm[t+ 32];
    for(int t=0;t<NORM_BLOCKSIZE;++t;@inner(0)) if(t< 16 && t+ 16<NORM_BLOCKSIZE) s_norm[t] += s_norm[t+ 16];
    for(int t=0;t<NORM_BLOCKSIZE;++t;@inner(0)) if(t<  8 && t+  8<NORM_BLOCKSIZE) s_norm[t] += s_norm[t+  8];
    for(int t=0;t<NORM_BLOCKSIZE;++t;@inner(0)) if(t<  4 && t+  4<NORM_BLOCKSIZE) s_norm[t] += s_norm[t+  4];
    for(int t=0;t<NORM_BLOCKSIZE;++t;@inner(0)) if(t<  2 && t+  2<NORM_BLOCKSIZE) s_norm[t] += s_norm[t+  2];
    for(int t=0;t<NORM_BLOCKSIZE;++t;@inner(0)) if(t<  1 && t+  1<NORM_BLOCKSIZE) norm[b] = s_norm[0] + s_norm[1];
  }


  for(dlong b=0;b<1;++b;@outer(0)){

    @shared dfloat s_norm[NORM_BLOCKSIZE];

    for(int t=0;t<NORM_BLOCKSIZE;++t;@inner(0)){
      dlong id = t;
      dfloat r_norm = 0.0;
      while (id<Nblocks) {
        r_norm += norm[id];
        id += NORM_BLOCKSIZE;
      }
      s_norm[t] = r_norm;
    }

#if NORM_BLOCKSIZE>512
    for(int t=0;t<NORM_BLOCKSIZE;++t;@inner(0)) if(t<512 && t+512<NORM_BLOCKSIZE) s_norm[t] += s_norm[t+512];
#endif
#if NORM_BLOCKSIZE>256
    for(int t=0;t<NORM_BLOCKSIZE;++t;@inner(0)) if(t<256 && t+256<NORM_BLOCKSIZE) s_norm[t] += s_norm[t+256];
#endif
    for(int t=0;t<NORM_BLOCKSIZE;++t;@inner(0)) if(t<128 && t+128<NORM_BLOCKSIZE) s_norm[t] += s_norm[t+128];
    for(int t=0;t<NORM_BLOCKSIZE;++t;@inner(0)) if(t< 64 && t+ 64<NORM_BLOCKSIZE) s_norm[t] += s_norm[t+ 64];
    for(int t=0;t<NORM_BLOCKSIZE;++t;@inner(0)) if(t< 32 && t+ 32<NORM_BLOCKSIZE) s_norm[t] += s_norm[t+ 32];
    for(int t=0;t<NORM_BLOCKSIZE;++t;@inner(0)) if(t< 16 && t+ 16<NORM_BLOCKSIZE) s_norm[t] += s_norm[t+ 16];
    for(int t=0;t<NORM_BLOCKSIZE;++t;@inner(0)) if(t<  8 && t+  8<NORM_BLOCKSIZE) s_norm[t] += s_norm[t+  8];
    for(int t=0;t<NORM_BLOCKSIZE;++t;@inner(0)) if(t<  4 && t+  4<NORM_BLOCKSIZE) s_norm[t] += s_norm[t+  4];
    for(int t=0;t<NORM_BLOCKSIZE;++t;@inner(0)) if(t<  2 && t+  2<NORM_BLOCKSIZE) s_norm[t] += s_norm[t+  2];
    for(int t=0;t<NORM_BLOCKSIZE;++t;@inner(0)) if(t<  1 && t+  1<NORM_BLOCKSIZE) norm[0] = s_norm[0] + s_norm[1];
  }
}
